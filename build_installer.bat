@echo off
REM Build JellyRip.exe and package JellyRipInstaller.exe with Inno Setup

set "PYTHON_EXE=.venv\Scripts\python.exe"
if not exist "%PYTHON_EXE%" set "PYTHON_EXE=python"
set ISCC_EXE=C:\Program Files (x86)\Inno Setup 6\ISCC.exe
if not exist "%ISCC_EXE%" set ISCC_EXE=C:\Program Files\Inno Setup 6\ISCC.exe
if not exist "%ISCC_EXE%" set ISCC_EXE=%LOCALAPPDATA%\Programs\Inno Setup 6\ISCC.exe
set "ARTIFACT_DIR=dist\main"
set "BUILD_DIR=build\main"

if exist "%ARTIFACT_DIR%" rmdir /s /q "%ARTIFACT_DIR%" >nul 2>&1
if exist "%BUILD_DIR%" rmdir /s /q "%BUILD_DIR%" >nul 2>&1
if not exist "%ARTIFACT_DIR%" mkdir "%ARTIFACT_DIR%"
type nul > "%ARTIFACT_DIR%\.gitkeep"

echo Building JellyRip.exe...
%PYTHON_EXE% -m PyInstaller --distpath "%ARTIFACT_DIR%" --workpath "%BUILD_DIR%" JellyRip.spec
if errorlevel 1 (
    echo EXE build failed.
    exit /b 1
)

powershell -NoProfile -ExecutionPolicy Bypass -File tools\stage_ffmpeg_bundle.ps1 -DistDir "%ARTIFACT_DIR%"
if errorlevel 1 (
    echo FFmpeg bundle staging failed.
    exit /b 1
)

if not exist "%ISCC_EXE%" (
    echo Inno Setup compiler not found.
    echo Install Inno Setup 6, then run this script again.
    exit /b 1
)

echo Building JellyRipInstaller.exe...
"%ISCC_EXE%" installer\JellyRip.iss
if errorlevel 1 (
    echo Installer build failed.
    exit /b 1
)

echo.
echo Build complete:
echo   %ARTIFACT_DIR%\JellyRip.exe
echo   %ARTIFACT_DIR%\JellyRipInstaller.exe
echo   %ARTIFACT_DIR%\ffmpeg.exe
echo   %ARTIFACT_DIR%\ffprobe.exe
echo   %ARTIFACT_DIR%\ffplay.exe
echo   %ARTIFACT_DIR%\FFmpeg-LICENSE.txt
echo   %ARTIFACT_DIR%\FFmpeg-README.txt
